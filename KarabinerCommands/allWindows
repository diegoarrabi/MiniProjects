#!/usr/bin/osascript

on windowleft()
	tell application "System Events"
		set _WINDOW to front window of (first application process whose frontmost is true)
		set window_pos to position of _WINDOW
		set finalPosition to {0, item 2 of window_pos}
		tell _WINDOW to set {position} to {finalPosition}
	end tell
end windowleft

on windowup()
    tell application "System Events"
        set _window to front window of (first application process whose frontmost is true)
        set window_pos to position of _window
        set finalPosition to {item 1 of window_pos, 0}
        tell _window to set {position} to {finalPosition}
    end tell
end windowup

on windowright()
    tell application "System Events"
        tell application "Finder" to set screen_size to bounds of window of desktop
        set _WINDOW to front window of (first application process whose frontmost is true)
        set window_pos to position of _WINDOW
        set window_size to size of _WINDOW
        set finalPositionX to ((item 3 of screen_size) - (item 1 of window_size))
        set finalPosition to {finalPositionX, item 2 of window_pos}
        tell _WINDOW to set {position} to {finalPosition}
    end tell
end windowright

on windowdown()
    tell application "System Events"
        tell application "Finder" to set screen_size to bounds of window of desktop
        set _WINDOW to front window of (first application process whose frontmost is true)
        set window_pos to position of _WINDOW
        set window_size to size of _WINDOW
        set finalPositionY to ((item 4 of screen_size) - (item 2 of window_size))
        set finalPosition to {item 1 of window_pos, finalPositionY}
        tell _WINDOW to set {position} to {finalPosition}
    end tell
end windowdown

on run(arg)
	set arg to item 1 of arg
	if arg is equal to "left" then
		windowleft()
	else if arg is equal to "up" then
		windowup()
	else if arg is equal to "right" then
		windowright()
	else if arg is equal to "down" then
		windowdown()
	end if
end run